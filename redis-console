#!/usr/bin/perl

use strict;
use warnings;

use RedisConsole;
use Getopt::Long::Descriptive;

my ($opt, $usage) = describe_options(
    'redis-console %o',
    [ 'host|h=s',       'Server hostname (default: 127.0.0.1)',    { default  => '127.0.0.1' } ],
    [ 'port|p=i',       'Server port (default: 6379)',             { default  => 6379 }        ],
    [ 'execute|e=s@',   'Execute Redis command (or commands)'                                  ],
    [ 'conf|c=s',       'Config file (default: ~/.redis-console)',
                        { default => "$ENV{HOME}/.redis-console" } ],
    [                                                                                          ],
    [ 'help',           'print usage message and exit'                                         ],
);

if ($opt->help) {
    print $usage->text;
    exit 1;
}

my $rc = RedisConsole->new(name => 'redis-console');
die 'dont know how to connect' unless $rc->can('cmd_connect');

my $conf_file = $opt->conf;
if ($opt->host && $opt->port) {
    $rc->execute_command('connect', $opt->host, $opt->port);
} elsif (-f $conf_file) {
    # TODO
}

# short cut in run command from command line
if ($opt->execute) {
    my $res = 0;
    foreach my $line (@{ $opt->execute }) {
        $res = $rc->execute($line);
    }

    exit($res);
}

my $hist_file = $opt->conf . '-history';
if (open(my $fh, '<', $hist_file)) {
    chomp, $rc->add_history($_) while (<$fh>);
    close($fh);
}

$SIG{INT} = $SIG{TERM} = sub {
    dump_history($hist_file, $rc->history);
    exit 0;
};

my $res = $rc->repl();
dump_history($hist_file, $rc->history);
exit $res;

sub dump_history {
    my ($file, $history) = @_;
    if (open(my $fh, '>', $file)) {
        print $fh join("\n", @{ $history });
        close($fh);
    }
}
